load ybuilder.core.base;
load ybuilder.core.build;
load ybuilder.core.extensiontest;

//TODO: Remove
(||) parentPath component = (path parentPath component)~;



project.artifactId := "ybuilder";
project.groupId :="ybuilder";
project.version := ybuilderVersion;
project.description := "A simple build tool for yeti";

for ["test1", "test2"] (push extensionTestDirs);

init = target "ybuilderinit" [InLivecycle livecycleTargets.initialize] do p:

    antTask "delete" ["dir":dir.main~] [] p;
    
    antTask "mkdir" ["dir":dir.java~] [] p;
    
    antTask "copy" ["todir":dir.java || "/ybuilder"] [
        el "fileset" ["dir":dir.ybuilder~] [
            el "include" ["name":"**/*.java"] []]] p;

    antTask "copy" ["todir":dir.yeti || "/ybuilder"] [
        el "fileset" ["dir":dir.ybuilder~] [
            el "include" ["name":"core/*.yeti"] []]] p;

    antTask "copy" ["todir":dir.resources~] [
        el "fileset" ["dir":dir.ybuilder || "/resources"] []] p;

    done;

jarFile = "ybuilder.jar";
    
jary = target "jary" [InLivecycle livecycleTargets.package] do p:
    jars = ["ant-1.8.1.jar","ant-launcher.jar","maven-ant-tasks-2.1.3.jar","yeti.jar"];
    
    antTask "jar" ["jarfile":jarFile,"basedir":dir.targetClasses~] 
            ((map do j:	
                    el "zipfileset" 
                       ["src":dir.lib || "/\(j)","excludes":"MANIFEST.MF"] [] 
                  done jars) 
             ++ [el "manifest" [:]
                    [el "attribute" 
                        ["name":"Implementation-Vendor", "value":project.vendor] 
                        [],
                    el "attribute" 
                       ["name":"Implementation-Title", 
                        "value":"\(project.groupId)-\(project.artifactId)"] 
                       [],
                    el "attribute" 
                       ["name":"Implementation-Version", "value":project.version] 
                       [],
                    el "attribute" 
                       ["name":"Main-Class", "value":"ybuilder.core.ybuilder"] 
                       []]]) 
            p;	
done;
            

dependency "junit" "junit" "3.8.2" [TestScope()];
dependency "org.apache.commons" "commons-io" "1.3.2" [];
dependency "javax.servlet" "servlet-api" "2.4" [ProvidedScope()];

//runTarget retrieveDependencies;

load ybuilder.core.run;