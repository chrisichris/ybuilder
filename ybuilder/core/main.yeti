/*
 * Copyright 2011 Christian Essl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

program ybuilder.core.main;

import java.lang:Thread,System;
import java.io:File;
import java.net:URLClassLoader;
import yeti.lang.compiler.yeti;

_ = System#setProperty("ybuilder.starttime", 
                        string System#currentTimeMillis());

load ybuilder.core.base;
build = load ybuilder.core.build;

file n = new File(n is string);
args = list _argv;

    
_withBuildClassLoader dirPrefix antFiles fn = 
   (load ybuilder.core.build;
    import org.apache.tools.ant.AntClassLoader;
    import org.apache.tools.ant.types: FileSet;
    oL = Thread#currentThread()#getContextClassLoader();
    
    flUrl = (classOf AntClassLoader)#getProtectionDomain()
                                    #getCodeSource()#getLocation();
        
        
    urls = antFiles
     |>map do n: (new File(dirPrefix ^ string n))#toURI()#toURL() done;

    ctxtLoader = if empty? urls then
            oL
        else
            import java.lang.ClassLoader;
            new URLClassLoader(array (flUrl::urls),
                               ClassLoader#getSystemClassLoader()#getParent()); 
        fi;
    Thread#currentThread()#setContextClassLoader(ctxtLoader);
    try
        fn ctxtLoader;
    finally 
        Thread#currentThread()#setContextClassLoader(oL);
    yrt);


    
if not (file "project.yeti")#exists() then
    //no project file create default project with some default targets
    load ybuilder.core.build;
    config = createBuildConfig (); 
    config = config with registerBuildTargets config;

    _ = target config "new" "newproject" 
            [Description "creates a basic new project",
            ShortName()] do p:
            
        load ybuilder.core.base;
        projectName = 
            if not empty? (tail p.args) then 
                (head (tail p.args)) 
            else 
                "."; 
            fi;
        config = createBaseConfig (baseConfig()) [];
        createNewYetiProject config projectName (createProject ()); 
        retrieveBuildDependencies ();

    done;

    _ = target config "new" "new"
        [Description "creates a new project from a git repo",
        ShortName()] do p:
        
        projectDir = if length p.args > 0 then
                head p.args;
            else 
                print "\nPlease enter the project's name: ";
                strTrim (readln());
            fi;
        
        
        if (file projectDir)#exists() then
            println "error a directory with name \(projectDir) already exists";
            System#exit(-1);
        fi;

        name = if length p.args > 1 then
                    "(Some '\((array p.args)[1])')"
               else "none" fi;
                    
        
        ///retrieve git dependencies
        import java.nio.file.Files;
        tempDir = Files#createTempDirectory("ybuilder_gitjars", [])#toFile();
        tempDir#deleteOnExit();
        
        project = createProject ();
     
        maven str = ("antlib:org.apache.maven.artifact.ant " ^ str);
     
        antTask (maven "dependencies") 
                ["filesetId" : "dependenciesFS", "useScope":"runtime"] 
                [el (maven "localRepository") 
                     ["layout":"default", 
                      "path":"${user.home}/.m2/repository"] [], 
                 el (maven "dependency") 
                       ["groupId" : "org.eclipse.jgit",
                        "artifactId" : "org.eclipse.jgit",
                        "version" :"1.3.0.201202151440-r",
                        "scope" : "runtime"]
                       []]
                project;
                    
        antTask "copy" ["todir":string tempDir] 
                [el "fileset" ["refid":"dependenciesFS"][],
                 el "mapper" ["type":"flatten"] []] 
                project;

        //load the template
         antFiles = listFiles (string tempDir) [Include "*.jar"];
       
        _withBuildClassLoader (string tempDir ^ string File#separator) 
                antFiles do ctxtLoader:
            m = ctxtLoader#loadClass("yeti.lang.compiler.yeti")
                          #getMethod("main",array [classOf java.lang.String[]]);
            _ = m#invoke((),
                    (array 
                        [(array ["-e", "(load ybuilder.core.projecttemplate).newFromTemplate \"\(projectDir)\" \(name)"]) as ~String[]]
                    ) as ~String[][]);
        done;

        //run the newproject file
        if (new File(projectDir^"/newproject.yeti"))#exists() then
            antFiles = 
                ((load ybuilder.core.build).listFiles) projectDir
                    [Include "\(buildDependenciesConfig.ybuilder)extlib/*.jar", 
                     Include 
                        "\(buildDependenciesConfig.ybuilderLibsManaged)*.jar"];

            _withBuildClassLoader "" antFiles do ctxtLoader:
                m = ctxtLoader#loadClass("yeti.lang.compiler.yeti")
                        #getMethod("main",array [classOf java.lang.String[]]);
                _ = m#invoke((),
                        (array 
                            [(array (projectDir^"newproject.yeti" :: p.args)) 
                                as ~String[]]
                        ) as ~String[][]);
            done;
            //delete the file
            antTask "delete" ["file":"newproject.yeti"] [] project;
        fi;

        println "new project successful";
        
    done;


    emptyHelp = target config "new" "help" 
            [Description "help shown when there is no project.yeti"] do p:
        println "ybuilder - version \(ybuilderVersion)\n";
        println ' 
    ybuilder is a simple build-enviroment for YETI the functional 
    programming-language for JVM.

    To use ybuilder create a new ybuilder project:
 
    1.) run "java -jar ybuilder.jar new projectName chrisichris/basic" 
        This will create a new directory "projectName" based on the
        github repository chrisichris/basic.ybtr
    2.) edit the "project.yeti" file in the new directory 
        as described in the comments
    3.) copy your ybuilder.jar to the new directy (if it is not present)
    4.) run "java -jar ybuilder.jar" again to get a detailed help message

    For detailed description google ybuilder yeti (there is no homepage yet)
    '
    done;

    config.defaultTarget := Some emptyHelp;

    run config;
else
    load ybuilder.core.build;
    project = createProject();
    //project file exists
    //so run it
    if not 
        (file build.buildDependenciesConfig.ybuilderLibsManaged)#exists() 
        then
        build.retrieveBuildDependencies ();
    fi;
    
    //check for project classes lib
    projectClassesDir = buildDependenciesConfig.ybuilderProjectClasses;
    projectClassFile = new File(file projectClassesDir, "project.class");
    
    if not projectClassFile#exists() 
        or projectClassFile#lastModified() 
            < (file "project.yeti")#lastModified() then
        //clean/create the target dir    
        antTask "delete" ["dir":projectClassesDir] [] project;
        antTask "mkdir" ["dir":projectClassesDir] [] project;
       
        println "--- compiling project.yeti ---------";
        //create classLoader with libs and compile project.yeti
        antFiles = listFiles "."
            [Include "\(buildDependenciesConfig.ybuilder)extlib/*.jar", 
             Include "\(buildDependenciesConfig.ybuilderLibsManaged)*.jar"];

        _withBuildClassLoader "" antFiles do ctxtLoader:
            m = ctxtLoader#loadClass("yeti.lang.compiler.yeti")
                        #getMethod("main",array [classOf java.lang.String[]]);
            pars = ["-d",projectClassesDir,"project.yeti"];
            _ = m#invoke((), (array [pars as ~String[]]) as ~String[][]);
        done;
    fi;
    
    //now we have the compiled project.class we run it now
    println "--- running project.yeti ----------";
    
    antFiles = listPath 
        [Location projectClassesDir,
         Fileset 
            [".": 
                [Include "\(buildDependenciesConfig.ybuilder)extlib/*.jar", 
                Include "\(buildDependenciesConfig.ybuilderLibsManaged)*.jar"]
            ]];
    
    _withBuildClassLoader "" antFiles do ctxtLoader:
        import java.lang.reflect:InvocationTargetException;
        m = ctxtLoader#loadClass("project")
                    #getMethod("main",array [classOf java.lang.String[]]);
        try
            m#invoke((),
                (array [(array args) as ~String[]]) as ~String[][]);
        catch InvocationTargetException ex:
            throw ex#getCause();
        yrt
    done;    
    
fi;

        
System#exit(0);
();



