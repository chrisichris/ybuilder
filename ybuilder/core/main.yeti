/*
 * Copyright 2011 Christian Essl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

program ybuilder.core.main;

load ybuilder.core.base;
build = load ybuilder.core.build;

import java.lang.Thread;
import java.io:File;
import java.net:URLClassLoader;
import yeti.lang.compiler.yeti;

file n = new File(n is string);
args = list _argv;

config = build.buildDependenciesConfig;
    
_withBuildClassLoader dirPrefix antFiles fn = 
   (load ybuilder.core.build;
    import org.apache.tools.ant.AntClassLoader;
    import org.apache.tools.ant.types: FileSet;
    oL = Thread#currentThread()#getContextClassLoader();
    
    flUrl = (classOf AntClassLoader)#getProtectionDomain()
                                    #getCodeSource()#getLocation();
        
        
    urls = map do n: (new File(dirPrefix ^ string n))#toURL() done antFiles;
    ctxtLoader = if empty? urls then
            oL
        else
            import java.lang.ClassLoader;
            new URLClassLoader(array (flUrl::urls),
                               ClassLoader#getSystemClassLoader()#getParent()); 
        fi;
    Thread#currentThread()#setContextClassLoader(ctxtLoader);
    try
        fn ctxtLoader;
    finally 
        Thread#currentThread()#setContextClassLoader(oL);
    yrt);

    
if (file "project.yeti")#exists() 
    and not (file config.dir.ybuilderLibsManaged)#exists() then
    build.retrieveBuildDependencies config;
fi;	


if (not (file "project.yeti")#exists()) and empty? args then
    yv = (load ybuilder.core.build).ybuilderVersion ;
    println "ybuilder - version \(yv)\n";
    println ' 
ybuilder is a simple build-enviroment for YETI the functional 
programming-language for JVM.

To use ybuilder create a new ybuilder project:

1.) copy the "ybuilder.jar" file to a new directory and 
2.) run "java -jar ybuilder.jar new chrisichris/basic" to create 
    the needed files and directories from the basic template
3.) edit the "project.yeti" file as described in the comments
4.) run "java -jar ybuilder.jar" again to get a detailed help message

For detailed description google ybuilder yeti (there is no homepage yet)
'
elif not (file "poject.yeti")#exists() then
    load ybuilder.core.ifnoproject;    
elif (not empty? args) and ((head args) == "cleanBuildLibs") then
    build.antTask "delete" 
                ["dir":config.dir.ybuilderLibsManaged] 
                [] 
                (build.createProject());
elif (not empty? args) and ((head args) == "retrieveBuildLibs") then
    build.retrieveBuildDependencies config;
else
    antFiles = 
        ((load ybuilder.core.build).listFiles) "."
            [Include "\(config.dir.ybuilder)extlib/*.jar", 
             Include "\(config.dir.ybuilderLibsManaged)*.jar"];

    _withBuildClassLoader "" antFiles do ctxtLoader:
        m = ctxtLoader#loadClass("yeti.lang.compiler.yeti")
                      #getMethod("main",array [classOf java.lang.String[]]);
        m#invoke((),
                (array 
                    [(array ("project.yeti" :: args)) as ~String[]]
                ) as ~String[][]);
    done;
fi;


        
System#exit(0);
();



