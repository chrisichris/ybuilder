/*
 * Copyright 2011 Christian Essl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

module ybuilder.core.projecttemplate;

import java.util:Properties, 
                 LinkedHashSet,
                 Enumeration,
                 Collections,
                 Set;

base = load ybuilder.core.base;
load ybuilder.core.build;


//cloning a default project

newFromTemplate projectDir nameOpt=
   (project = createProject();
    println "--------------------------\nybuilder project template download\n";
    println "installation happens in [\(projectDir)] directory";

    name = case nameOpt of
    None _: 
        (print "\nPlease enter the location of the repository: ";
         strTrim (readln ()));
    Some n: n;
    esac;


    //parse the name
    {url,dir} = if strStarts? name "http::"
       or strStarts? name "https::"
       or strStarts? name "git::" then
            cut = strLastIndexOf name '#' 0;
            if cut < 0 then
                {url = name, dir = ""}
            else
                {url = strLeft name cut, 
                 dir = strRight name (cut + 1) }
            fi
    else
        strs = like '\A([^/]+)/([^/#]+)(#(.+))?\z' name ();
        if empty? strs then
            failWith "please provide a full name for the repo";
        else
            np = if not strEnds? strs.[2] ".ybtr" then
                    strs.[2]^".ybtr";
                else 
                    strs.[2];
                fi;
            {url = "git://github.com/\(strs.[1])/\(np).git",
             dir = strs.[4]}
        fi
    fi;

    import java.nio.file:Files;
    import java.io:File,
                   FileReader;
    import java.util.Properties;

    tempDir = Files#createTempDirectory(
                "ybuilder_templateproject", [])#toFile();
    tempDir#deleteOnExit();
    
    println "Cloning \(url) to \(tempDir) ...";
    try
        //clone the project
        import org.eclipse.jgit.api:Git;
        _ = Git#cloneRepository()#setURI(url)#setDirectory(tempDir)#call();
    catch Exception ex:
        failWith "Could not clone repository at \(url): \(ex)";
    yrt;
    
    //the directory form where to copy all files
    sourceDir = if dir == "" then   
            tempDir
        else
            new File(tempDir, dir);
        fi;

    println "Succesfully Cloned \(url) to \(tempDir)";   
        
    if not sourceDir#isDirectory() then
        failWith "No subdir in project \(url) of name \(dir)";
    fi;
    

    
    //properties to replace
    props = readProperties (new File(sourceDir, "ybtemplate.properties"));
    {names, values} = if not "name" in props.values then
        props.values["name"] := projectDir;
        {names = "name" :: props.names,
         values = props.values}
    else
        props
    fi;

    resProps = [:];
    
    readProp k =
        if (k is string) in resProps then
            resProps.[k];
        else    
            vue = values.[k];
            v = 
               (va = like '^@([^@]+)@$' vue ();
                if empty? va then
                    vue
                else
                    readProp va.[1]
                fi);
                
            println "\(k) [\(v)]:";
            rv = readln ();
            ov = if rv == "" then 
                    v
                 else 
                    rv 
                 fi;
            
            resProps.[k] := ov;
            ov;
        fi;
    for names (\() . readProp);    
    
    
    antTask "copy" ["todir":projectDir, 
                    "verbose":"true","includeEmptyDirs":"true"] [
        el "fileset" ["dir":string sourceDir, 
                      "defaultexcludes":"false", 
                      "excludes":"**/.git/**"] [],
        el "filterchain" [:] [
            el "replacetokens" [:] 
                (mapHash do k v:
                    el "token" ["key":k, "value":v] [] done resProps)
        ]
    ] project;

    
    antTask "delete" ["file":projectDir^"/ybtemplate.properties", 
                      "failonerror":"false",
                      "deleteonexit":"true"][] project;
    
    //write the properties
    import java.io.FileWriter;
    wr = new FileWriter("\(projectDir)/ybtr.properties");
    propsObj = new Properties();
    forHash resProps do k v:
        _ = propsObj#put(k as ~Object, v as ~Object);
    done;
    try
        propsObj#store(wr, "");
    finally
        wr#close();
    yrt);

 {
    newFromTemplate,
 }
