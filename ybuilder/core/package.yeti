module ybuilder.core.package;

load ybuilder.core.base;
load ybuilder.core.build;


jarManifest = [:] is hash<string,string>;
jarAttributes = 
    ["destfile":targetDir || "/\(projectGroupId)-\(projectVersion).jar"];

jarFileset = [ targetClassesDir~ : []];

warAttributes = 
    ["destfile":targetDir || "/\(projectGroupId).war"];

warLibset = 
     [libDir~ : [Include "**/*.jar"],
      libManagedDir~ : [Include "compile/**/*.jar",
                        Include "runtime/**/*.jar"]];
                       
warClassesset =
    [targetClassesDir~ : [Include "**/*.*"]];

warFileset = [webappDir~ : [Include "**/*.*"]];

webxmlFile = path webappDir "/WEB-INF/web.xml";

{
    jarManifest,
    jarAttributes,
    jarFileset,
    
    warAttributes,
    warLibset,
    warClassesset,
    warFileset,
    webxmlFile,
    
    jar = 
        target "jar" 
               [Depends preparePackage, 
                Description "Makes a jar file in \(jarAttributes.['destfile'])"] 
               (antTask "jar" jarAttributes 
                        (mapHash do dir ie:
                            antFileset dir ie;
                         done jarFileset
                         ++ [el "manifest" [:] 
                               (mapHash do k v: 
                                 el "attribute" ["name":k, "value":v] [] 
                               done jarManifest)] )),

    war = 
        target "war" 
               [Depends preparePackage, 
                Description "Makes a war file in \(warAttributes.['destfile'])"]
               (antTask "war" 
                        (atts = (copy warAttributes);
                         atts.["webxml"]:= (webxmlFile~);
                         atts)
                        ((mapHash do dir ie:
                                el "lib" [dir:"."] (antPatterns ie);
                          done warLibset)
                         ++ (mapHash do dir ie:
                                el "classes" [dir:"."] (antPatterns ie);
                          done warClassesset)
                         ++ (mapHash do dir ie:
                                antFileset dir ie;
                          done warFileset)))
}
